plugins {
    id 'com.android.application'
    id 'kotlin-android'
}
String sdkVersion = "0.2.449"

def localPropertyFile = project.rootProject.file("local.properties")
Properties localProperties = new Properties()
if (localPropertyFile.canRead()) {
    localPropertyFile.newDataInputStream().withCloseable {
        localProperties.load(it)
    }
}

String navigationApiKey = localProperties.get("navigationApiKey") ?: findProperty("navigationApiKey")
String routingApiKey = localProperties.get("routingApiKey") ?: findProperty("routingApiKey")
String mapsApiKey = localProperties.get("mapsApiKey") ?: findProperty("mapsApiKey")

if (!routingApiKey?.trim() || !navigationApiKey?.trim() || !mapsApiKey?.trim()) {
    throw new GradleException("Make sure to add api keys to your local.properties file:\n" +
            "routingApiKey=yourKey\n" +
            "navigationApiKey=yourKey\n" +
            "mapsApiKey=yourKey")
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.motomized.navigation"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName version

        resValue "string", "navigationApiKey", "$navigationApiKey"
        resValue "string", "routingApiKey", "$routingApiKey"
        resValue "string", "mapsApiKey", "$mapsApiKey"
        resValue "string", "sdkVersionLabel", "SDK version: $sdkVersion"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        pickFirst "lib/**/libc++_shared.so"
    }

    //  keyPassword and storePassword are now just random strings, they are NOT correct.
    signingConfigs {
        release {
            keyAlias 'xplorer-key'
            keyPassword "releaseKeyPassword"
            storeFile file("$rootDir/signing/release-key.keystore")
            storePassword "releaseStorePassword"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'com.markodevcic:peko:2.1.4'
    implementation "com.tomtom.sdk:maps-display:$sdkVersion"
    implementation "com.tomtom.sdk:navigation-ui:$sdkVersion"
    implementation "com.tomtom.sdk:navigation:$sdkVersion"
    implementation "com.tomtom.sdk:routing-client:$sdkVersion"
    implementation "com.tomtom.sdk:location-android:$sdkVersion"
    implementation "com.tomtom.sdk:location-simulation:$sdkVersion"
    implementation "com.tomtom.sdk:location-mapmatched:$sdkVersion"

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "com.jakewharton.timber:timber:5.0.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
